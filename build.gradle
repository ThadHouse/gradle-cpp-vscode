plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'groovy'
    id "edu.wpi.first.wpilib.versioning.WPILibVersioningPlugin" version "2.2"
    id "com.gradle.plugin-publish" version "0.10.0"
}

// Ensure that the WPILibVersioningPlugin is setup by setting the release type, if releaseType wasn't
// already specified on the command line
if (!hasProperty('releaseType')) {
    WPILibVersion {
        releaseType = 'dev'
    }
}

ext.pubVersion = ''
if (project.hasProperty("publishVersion")) {
    pubVersion = project.publishVersion
} else {
    pubVersion = WPILibVersion.version
}

if (pubVersion == '') {
    pubVersion = '0.0.1-unknown'
}

if (project.hasProperty('localPublish')) {
    pubVersion = '424242.0.0-local'
}

group 'edu.wpi.first'
version pubVersion

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenLocal()
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

dependencies {
    compile 'com.google.code.gson:gson:2.2.4'

    testCompile('org.spockframework:spock-core:1.2-groovy-2.5') {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile gradleTestKit()
}


pluginBundle {
    website = 'https://github.com/wpilibsuite/gradle-cpp-vscode'
    vcsUrl = 'https://github.com/wpilibsuite/gradle-cpp-vscode'
    description = 'This plugin provides VS Code support for old stylde gradle projects.'
    tags = ['groovy', 'vscode', 'cpp', 'maven', 'frc', 'wpilib']
}

gradlePlugin {
    plugins {
        GradleVsCode {
            id = 'edu.wpi.first.GradleVsCode'
            implementationClass = 'edu.wpi.first.vscode.GradleVsCode'
        }
    }
}

wrapper {
    gradleVersion = '5.0'
}
